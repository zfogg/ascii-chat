#compdef ascii-chat

_ascii-chat-client() {
    _arguments \
        '(-h --help)'{-h,--help}'[print this help]' \
        '(-a --address)'{-a,--address}'[IPv4 address to connect to]' \
        '(-p --port)'{-p,--port}'[TCP port]' \
        '(-x --width)'{-x,--width}'[render width]' \
        '(-y --height)'{-y,--height}'[render height]' \
        '(-H --host)'{-H,--host}'[hostname for DNS lookup]' \
        '(-c --webcam-index)'{-c,--webcam-index}'[webcam device index (0-based)]' \
        '--list-webcams[list available webcam devices and exit]' \
        '--list-microphones[list available audio input devices and exit]' \
        '--list-speakers[list available audio output devices and exit]' \
        '(-f --webcam-flip)'{-f,--webcam-flip}'[toggle horizontal flip of webcam image]' \
        '--test-pattern[use test pattern instead of webcam]' \
        '--fps[desired frame rate 1-144]' \
        '--color-mode[color modes]:(auto none 16 256 truecolor)' \
        '--show-capabilities[show detected terminal capabilities and exit]' \
        '--utf8[force enable UTF-8/Unicode support]' \
        '(-M --render-mode)'{-M,--render-mode}'[rendering mode]:(foreground background half-block)' \
        '(-A --audio)'{-A,--audio}'[enable audio capture and playback]' \
        '(-s --stretch)'{-s,--stretch}'[stretch or shrink video to fit]' \
        '(-q --quiet)'{-q,--quiet}'[disable console logging]' \
        '(-S --snapshot)'{-S,--snapshot}'[capture single frame and exit]' \
        '(-D --snapshot-delay)'{-D,--snapshot-delay}'[delay SECONDS before snapshot]' \
        '--mirror[view webcam locally without network]' \
        '--strip-ansi[remove all ANSI escape codes]' \
        '--server-key[expected server public key]:file:_files' \
        '(-P --palette)'{-P,--palette}'[ASCII character palette]:(standard blocks digital minimal cool custom)' \
        '(-C --palette-chars)'{-C,--palette-chars}'[Custom palette characters]' \
        '(-L --log-file)'{-L,--log-file}'[redirect logs to file]:file:_files' \
        '--log-level[set log level]:(dev debug info warn error fatal)' \
        '*'{-V,--verbose}'[increase log verbosity (stackable)]' \
        '(-E --encrypt)'{-E,--encrypt}'[enable packet encryption]' \
        '(-K --key)'{-K,--key}'[SSH/GPG key for authentication]:file:_files' \
        '(-F --keyfile)'{-F,--keyfile}'[read encryption key from file]:file:_files' \
        '--password[password for connection encryption]' \
        '--no-encrypt[disable encryption]'
}

_ascii-chat-server() {
    _arguments \
        '(-h --help)'{-h,--help}'[print this help]' \
        '(-a --address)'{-a,--address}'[IPv4 address to bind to]' \
        '--address6[IPv6 address to bind to]' \
        '(-p --port)'{-p,--port}'[TCP port]' \
        '--client-keys[allowed client keys file]:file:_files' \
        '(-P --palette)'{-P,--palette}'[ASCII character palette]:(standard blocks digital minimal cool custom)' \
        '(-C --palette-chars)'{-C,--palette-chars}'[Custom palette characters]' \
        '(-L --log-file)'{-L,--log-file}'[redirect logs to file]:file:_files' \
        '--log-level[set log level]:(dev debug info warn error fatal)' \
        '*'{-V,--verbose}'[increase log verbosity (stackable)]' \
        '(-E --encrypt)'{-E,--encrypt}'[enable packet encryption]' \
        '(-K --key)'{-K,--key}'[SSH/GPG key for authentication]:file:_files' \
        '(-F --keyfile)'{-F,--keyfile}'[read encryption key from file]:file:_files' \
        '--password[password for connection encryption]' \
        '--no-encrypt[disable encryption]'
}

_ascii_chat() {
    local subcommand="${words[2]}"

    # If no subcommand yet, show subcommand options
    if [[ -z "$subcommand" || "$subcommand" == -* ]]; then
        _arguments \
            '(-h --help)'{-h,--help}'[print usage information and exit]' \
            '(-v --version)'{-v,--version}'[print version information and exit]' \
            '1:subcommand:(client server)'
        return
    fi

    # Adjust position context: remove the subcommand from words and decrement CURRENT
    # so the helper functions see arguments starting at position 1
    shift words
    (( CURRENT-- ))

    # Dispatch to appropriate subcommand completion function
    case "$subcommand" in
        client) _ascii-chat-client ;;
        server) _ascii-chat-server ;;
        *) _default ;;
    esac
}

_ascii_chat
